# import required libraries
from snakemake.utils import min_version


##### set minimum snakemake version #####
min_version("8.4.1")


containerized: "docker://ghcr.io/ht-diva/pqtl_pipeline_finemap:44f3df5"


# read the configuration file
configfile: "conf/config.yaml"
config_copy = "conf/config.yaml"


include: "rules/common.smk"
include: "rules/locus_breaker.smk"
include: "rules/cojo.smk"
include: "rules/coloc.smk"
include: "rules/return_config.smk"


def rule_all_inputs():
    inputs = []
    # Always run locus breaker
    inputs.append(rules.collect_loci.output[0])
    run_mode = config["run"]

    if run_mode in ["cojo_conditional", "coloc"]:
        inputs.append(rules.collect_conditionals.output.ofile)

    if run_mode == "coloc":
        inputs.extend(
            expand(
                ws_path("coloc/chr{chr_cs}_colocalization.table.all.tsv"),
                chr_cs=range(1, 23)
            )
        )

    # Always track used config
    inputs.append(ws_path("config/config_used.yaml"))
    return inputs


rule all:
    input:
        rule_all_inputs()
